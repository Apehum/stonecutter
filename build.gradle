plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version "1.2.0"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

version = '1.5-dev.9'
group = 'io.shcm.shsupercm.fabric'

def file = project.file("latest.version")
file.write(version)

dependencies {
    compileOnly project('idea-integration')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

test {
    useJUnitPlatform()
}

jar {
    from {
        project('idea-integration').sourceSets.main.output
    }
}

publishing {
    repositories {
        maven {
            name = "kikugieMaven"
            url = "https://maven.kikugie.dev/third-party"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = "stonecutter"
            version = version
            artifact jar
        }
    }
}

gradlePlugin {
    website = 'https://github.com/SHsuperCM/Stonecutter'
    vcsUrl = 'https://github.com/SHsuperCM/Stonecutter'

    plugins {
        stonecutter {
            id = 'io.shcm.shsupercm.fabric.stonecutter'
            implementationClass = 'io.shcm.shsupercm.fabric.stonecutter.StonecutterPlugin'
            displayName = "Stonecutter"
            description = 'Preprocessor/JCP inspired multi-version environment manager'
            tags.set(['fabric', 'fabricmc'])
        }
    }
}
javadoc.options.addStringOption('Xdoclint:none', '-quiet')